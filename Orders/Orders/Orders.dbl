
;;; <summary>
;;; The main entry point for the application.
;;; </summary>
main Orders

    .include "WND:windows.def"
    .include "INC:header.def"
    .include "INC:detail.def"

    record
        group now, a
            group date, d
                year,   d4
                month,  d2
                day,    d2
            endgroup
            group time, d
                hour,   d2
                minute, d2
                second, d2
            endgroup
        endgroup

        tt,             i4          ;Terminal channel
        achr,           a1          ;A character
        mnuWinId,       i4          ;Menu window ID
        orderWinId,     i4          ;Order window ID
        detailWinId,    i4          ;Order item window ID

        mh,             D_HANDLE    ;Dynamic memory handle
        ms,             i4          ;Memory size in records
        mc,             i4          ;Memory counter (used) in records

    endrecord

proc

    call startup
    call process
    call shutdown

    stop

;----------------------------------------------------------------
; Code to start and initialize the program environment
;
startup,
    now = %datetime

    open(tt=0,i,"tt:")
    xcall flags(87004020,1)

    xcall w_init(1,tt,5)

.ifndef D_GUI
    display(tt,$SCR_CLR(SCREEN))
.else
    xcall w_caption(WC_SET,"My First Synergy Application")
.endc

    return

;----------------------------------------------------------------
; Code to clean up and shut down the environment
;
shutdown,

    xcall w_exit

.ifndef D_GUI
    display(tt,$SCR_CLR(SCREEN))
.endc
    close tt

    return

;----------------------------------------------------------------
; Code to perform main processing
;
process,

    xcall CREATE_WINDOW(mnuWinId,"MENU",9,30,,,"Order Processing Menu")

    xcall DISPLAY_TEXT(mnuWinId,2,2,"[A]  Add a new order")
    xcall DISPLAY_TEXT(mnuWinId,4,2,"[V]  View an order")
    xcall DISPLAY_TEXT(mnuWinId,6,2,"[E]  Exit")
    xcall DISPLAY_TEXT(mnuWinId,8,2,"Select an option: ")

    repeat
    begin
        xcall WAIT_KEY(mnuWinId,8,20,achr)

        ;;Uppercase the key pressed
        upcase achr

        using achr select
        ("A"),
            call order_header
        ("V"),
            xcall DISPLAY_MESSAGE("You selected option V")
        ("E"),
            exitloop
        (),
            xcall DISPLAY_MESSAGE("You selected an invalid option!")
        endusing

    end

    xcall DELETE_WINDOW(mnuWinId)

    return

;----------------------------------------------------------------
; Code to create a new order
;
order_header,

    xcall CREATE_WINDOW(orderWinId,"ORDER",8,55,07,12,"Order Details")

    xcall DISPLAY_TEXT(orderWinId,2,2,"Order number")
    xcall DISPLAY_TEXT(orderWinId,4,2,"Customer account")
    xcall DISPLAY_TEXT(orderWinId,5,2,"Date of order")
    xcall DISPLAY_TEXT(orderWinId,6,2,"Placed by")
    xcall DISPLAY_TEXT(orderWinId,7,2,"Sales rep number")

    repeat
    begin
        xcall input(orderWinId,2,20,header.oh_number)

        ;Did the user just hit enter (to exit)?
        if (!header.oh_number)
            exitloop

        xcall input(orderWinId,4,20,header.oh_customer,true)
        xcall input(orderWinId,5,20,header.oh_date,,,true)
        xcall input(orderWinId,6,20,header.oh_contact,true)
        xcall input(orderWinId,7,20,header.oh_salesrep,true)

        ;Allocate dynamic memory for order items

        if (!mh) then
        begin
            mh = %mem_proc(DM_ALLOC,^size(strDetail)*(ms=3))
        end
        else
        begin
            if (ms>3)
                mh = %mem_proc(DM_RESIZ,^size(strDetail)*(ms=3),mh)
        end

        ;Go process items for the new order

        call order_detail

        ;Saving the order to the order data files

        xcall DISPLAY_MESSAGE("Save the details to data files")

        xcall CLEAR_BOX(orderWinId,4,20,7,55)
    end

    xcall DELETE_WINDOW(orderWinId)
    
    ;Deallocate dynamic memory
    mh = %mem_proc(DM_FREE,mh)

    return

;----------------------------------------------------------------
; Code to add items to a new order
;
order_detail,

    xcall CREATE_WINDOW(detailWinId,"DETAIL",10,55,10,15,"Order Details")
    
    xcall DISPLAY_TEXT(detailWinId,4,2,"Part number")
    xcall DISPLAY_TEXT(detailWinId,6,2,"Quantity")
    xcall DISPLAY_TEXT(detailWinId,8,2,"Unit price")

    mc = 0

    repeat
    begin
        ;;New line item
        if ((mc+=1)>ms)
        begin
            ;;Resize dynamic memory
            mh = %mem_proc(DM_RESIZ,^size(strDetail)*(ms+=3),mh)
        end

        xcall DISPLAY_TEXT(detailWinId,2,2,"Entering item " + %string(mc) + " of " + %string(ms))

        ^m(strDetail[mc].od_number,mh) = header.oh_number
        ^m(strDetail[mc].od_item,mh) = mc

        xcall input(detailWinId,4,15,^m(strDetail[mc].od_part,mh))

        if (!^m(strDetail[mc].od_part,mh))
        begin
            mc-=1
            exitloop
        end

        xcall input(detailWinId,6,15,^m(strDetail[mc].od_qty,mh),true)
        xcall input(detailWinId,8,15,^m(strDetail[mc].od_unit_price,mh),true)

        xcall CLEAR_BOX(detailWinId,4,15,8,55)

    end

    xcall DELETE_WINDOW(detailWinId)

    return

endmain