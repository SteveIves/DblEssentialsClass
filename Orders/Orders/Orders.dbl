
;;; <summary>
;;; The main entry point for the application.
;;; </summary>
main Orders

    .include "WND:windows.def"

    record header
        oh_number,      d6      ;Order number
        oh_customer,    a8      ;Customer account code
        oh_date,        d8      ;Order date (YYYYMMDD)
        oh_contact,     a20     ;Order placed by
        oh_salesrep,    d4      ;Sales rep code
    endrecord

    structure strDetail
        od_number,      d6      ;Order number
        od_item,        d4      ;Item number
        od_part,        a10     ;Part number
        od_qty,         d5      ;Quantity ordered
        od_unit_price,  d8.2    ;Unit price
    endstructure

    record
        detail,         strDetail
        group now, a
            group date, d
                year,   d4
                month,  d2
                day,    d2
            endgroup
            group time, d
                hour,   d2
                minute, d2
                second, d2
            endgroup
        endgroup

        tt,             i4
        achr,           a1
        msgwin,         i4

    endrecord

proc

    call startup
    call process
    call shutdown

    stop

;----------------------------------------------------------------
; Code to start and initialize the program environment
;
startup,
    now = %datetime

    open(tt=0,i,"tt:")
    xcall flags(7004020,1)
    display(tt,$SCR_CLR(SCREEN))

    xcall w_init(1,tt,5)
    xcall w_caption(WC_SET,"My First Synergy Application")

    return

;----------------------------------------------------------------
; Code to clean up and shut down the environment
;
shutdown,

    xcall w_exit

    display(tt,$SCR_CLR(SCREEN))
    close tt

    return

;----------------------------------------------------------------
; Code to perform main processing
;
process,

    ;Create a new window.
    xcall w_proc(WP_CREATE,msgwin,"MESSAGE",3,30)

    ;Give the window a title of “Message” and specify that the title is displayed
    ;in the center of the top border.
    xcall w_brdr(msgwin,
    &   WB_ON,
    &   WB_TPOS,WBT_TOP,WBT_CENTER,
    &   WB_TITLE,"Message")

    ;Display some text into the window.
    xcall w_disp(msgwin,WD_POS,2,2,"Welcome to my Synergy app!")

    ;Place the window onto the screen.
    xcall w_proc(WP_PLACE,msgwin,10,25)

    ;Allow the user to acknowledge the message by waiting for a key to be pressed.
    xcall w_disp(msgwin,WD_ACCEPT,achr)

    ;Delete the window.
    xcall w_proc(WP_DELETE,msgwin)

    return

endmain